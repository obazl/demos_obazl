load(
    "@rules_ocaml//build:rules.bzl",
    "ocaml_binary",
    "ocaml_module",
    "ocaml_test",
)
load("@rules_ppx//build:rules.bzl", "ppx_executable")

# In this example we have three modules, Main, Hello, and Goodbye,
# each compiled with a different ppx_inline_test "libname". This
# "libname" determines a "class" of tests; we can only run one at a
# time, by passing 'inline-test-runner <libname>' to the test
# executable. We use a string flag and config_settings so that we can
# pass the libname we want on the command line.

load("@bazel_skylib//rules:common_settings.bzl", "string_flag")

string_flag(name = "t",
            values = ["main", "hello", "goodbye"],
            build_setting_default = "main",
)
# Convert the flag passed to a config_setting that 'select()' can use:
config_setting(name = "main", flag_values = {":t": "main"})
config_setting(name = "hello", flag_values = {":t": "hello"})
config_setting(name = "goodbye", flag_values = {":t": "goodbye"})

# to select a test class, pass e.g.
# --//testing/ppx_inline_test/compound:t=hello
# as a convenience we've defined a flag alias in .bazelrc:
# build --flag_alias=ppxt=//testing/ppx_inline_test/compound:t
# so we can do this instead:
# --ppxt=hello

ocaml_test(
    name = "test",
    vm_linkage = "static",
    args = select({
        ":hello": ["inline-test-runner", "hello"],
        ":main": ["inline-test-runner", "main"],
        ":goodbye": ["inline-test-runner", "goodbye"],
        "//conditions:default": ["inline-test-runner", "main"]
    }) + [
        # "-list-test-names",
        # "-log",
        "-verbose"
    ],
    ## :Runner contains Ppx_inline_test_lib.Runtime.exit (); obazl
    ## puts 'main' last
    main = ":Runner",
    prologue = [
        "@opam.ppx_inline_test//runner/lib",
        ":Main",

        "@opam.time_now//lib" ## FIXME: this should automatically be added
    ],
    timeout = "short"
)

ocaml_module(
    name   = "Runner",
    struct = "runner.ml",
    deps   = [
        "@opam.ppx_inline_test//runtime-lib/lib",
    ],
)

ocaml_module(
    name = "Main",
    ppx = ":ppx.exe",
    ppx_args = [
        "-inline-test-lib", "main"
    ],
    struct = "main.ml",
    deps = [":Hello", ":Goodbye"]
)

ocaml_module(
    name = "Hello",
    ppx = ":ppx.exe",
    ppx_args = [
        "-inline-test-lib", "hello",
    ],
    struct = "hello.ml",
)

ocaml_module(
    name = "Goodbye",
    ppx = ":ppx.exe",
    ppx_args = [
        "-inline-test-lib", "goodbye"
    ],
    struct = "goodbye.ml",
)

################################################################
ppx_executable(
    name = "ppx.exe",
    main = ":Driver",
    opts = [],
    prologue = [
        "@opam.ppx_inline_test//lib",
        ## NB! ppx_inline_test itself depends on the runtime-lib
        "@opam.ppx_inline_test//runtime-lib/lib",
    ],
    ppx_codeps = [
        ## FIXME: these are listed as codeps in the ppx_inline_test
        ## pkg, so we should not need to list them here.
        "@opam.ppx_inline_test//runtime-lib/lib",
        "@opam.ppx_inline_test//config/lib",
    ]
)

#############
ocaml_module(
    name = "Driver",
    struct = ":ppxlib_driver.ml",
    visibility = ["//visibility:public"],
    deps = ["@opam.ppxlib//lib"],
)

########
genrule(
    name = "gendriver",
    outs = ["ppxlib_driver.ml"],
    cmd = "\n".join([
        "echo \"(* GENERATED FILE - DO NOT EDIT *)\" > \"$@\"",
        "echo \"let () = Ppxlib.Driver.standalone ()\" >> \"$@\"",
    ]),
)
